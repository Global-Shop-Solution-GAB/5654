Program.Sub.ScreenSU.Start
Gui.fmPDHist..Create
Gui.fmPDHist..Caption("Promise Date History")
Gui.fmPDHist..Size(15555,6855)
Gui.fmPDHist..MinX(0)
Gui.fmPDHist..MinY(0)
Gui.fmPDHist..Position(0,0)
Gui.fmPDHist..BackColor(-2147483633)
Gui.fmPDHist..MousePointer(0)
Gui.fmPDHist..Event(UnLoad,fmPDHist_UnLoad)
Gui.fmPDHist.GsPDHist.Create(GsGridControl)
Gui.fmPDHist.GsPDHist.Size(15555,6855)
Gui.fmPDHist.GsPDHist.Position(15,-15)
Gui.fmPDHist.GsPDHist.Anchor(13)
Gui.fmPDHist.GsPDHist.Event(CellValueChanged,GsPDHist_CellValueChanged)
Gui.fmRsn..Create
Gui.fmRsn..Caption("Please enter a reason")
Gui.fmRsn..Size(6840,2190)
Gui.fmRsn..MinX(0)
Gui.fmRsn..MinY(0)
Gui.fmRsn..Position(0,0)
Gui.fmRsn..BackColor(-2147483633)
Gui.fmRsn..MousePointer(0)
Gui.fmRsn.lblAlert.Create(Label,"The Promise Date has been modified.  Please enter a reason for this change.",True,6600,255,0,-15,90,True,0,"Arial",8,-2147483633,0)
Gui.fmRsn.lblAlert.FontStyle(True,False,False,False,False)
Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start
V.Global.sCPromDt.Declare'Current promise date on SO Line screen
V.Global.sOPromDt.Declare'Original promise date
V.Global.sNPromDt.Declare'New promise date
V.Global.iChgCnt.Declare'Change count
Program.Sub.Preflight.End

Program.Sub.Main.Start
F.Intrinsic.Control.Try
V.Local.sError.Declare
V.Local.iRETURN.Declare
'TJS, Sept 2019
'Customer: Bristol Machine Works, LTD.
'Business Case: Customer needs a way to record changes made 
'to the promise date on the sales order lines. They want to be able to analyze order shipments by comparing it to the original promise date.
'Solution: Global shop will save the original promise date when a line is added to the sales order with the user ID and save any subsequent changes as additional records in a custom database table. We will display this info in a dashboard and on the Sales Order Line screen.
'Associated hooks: 12705 (SO Line Change), 12706 (Post Clear), 12231 (SO Line Post Save), 12242 (SO Line Delete), 12431 (GAB-BTN-1), 12201 (Part change)
'Passed variables: 000011 (SO ITEM-PROM-DT), 000052 (SO PART_NUMBER), 000051 (SO Line Item), 000060 (ORDER-NUM), GAB-BTN-1, GAB-LBL-1 through GAB-LBL-4

F.Intrinsic.Variable.SetProperty("GAB-LBL-1","PL",15)
F.Intrinsic.Variable.SetProperty("GAB-LBL-1","PT",440)
F.Intrinsic.Variable.SetProperty("GAB-LBL-1","PH",15)
F.Intrinsic.Variable.SetProperty("GAB-LBL-1","PW",105)
F.Intrinsic.Variable.SetProperty("GAB-LBL-1","FS",15)
F.Intrinsic.Variable.SetProperty("GAB-LBL-2","PL",125)
F.Intrinsic.Variable.SetProperty("GAB-LBL-2","PT",440)
F.Intrinsic.Variable.SetProperty("GAB-LBL-2","PH",15)
F.Intrinsic.Variable.SetProperty("GAB-LBL-2","PW",75)
F.Intrinsic.Variable.SetProperty("GAB-LBL-2","FS",15)
F.Intrinsic.Variable.SetProperty("GAB-LBL-3","PL",15)
F.Intrinsic.Variable.SetProperty("GAB-LBL-3","PT",455)
F.Intrinsic.Variable.SetProperty("GAB-LBL-3","PH",15)
F.Intrinsic.Variable.SetProperty("GAB-LBL-3","PW",110)
F.Intrinsic.Variable.SetProperty("GAB-LBL-3","FS",15)
F.Intrinsic.Variable.SetProperty("GAB-LBL-4","PL",125)
F.Intrinsic.Variable.SetProperty("GAB-LBL-4","PT",455)
F.Intrinsic.Variable.SetProperty("GAB-LBL-4","PH",15)
F.Intrinsic.Variable.SetProperty("GAB-LBL-4","PW",50)
F.Intrinsic.Variable.SetProperty("GAB-LBL-4","FS",15)
F.Intrinsic.Variable.SetProperty("GAB-BTN-1","PL",215)
F.Intrinsic.Variable.SetProperty("GAB-BTN-1","PT",440)
F.Intrinsic.Variable.SetProperty("GAB-BTN-1","PH",25)
F.Intrinsic.Variable.SetProperty("GAB-BTN-1","PW",100)
F.Intrinsic.Variable.SetProperty("GAB-BTN-1","FS",15)
F.Intrinsic.Variable.SetProperty("GAB-LBL-1","FC",V.Color.LtRed)
F.Intrinsic.Variable.SetProperty("GAB-LBL-2","FC",V.Color.LtRed)
F.Intrinsic.Variable.SetProperty("GAB-LBL-3","FC",V.Color.LtRed)
F.Intrinsic.Variable.SetProperty("GAB-LBL-4","FC",V.Color.LtRed)
F.Intrinsic.Variable.SetProperty("GAB-BTN-1","PL",215)
F.Intrinsic.Variable.SetProperty("GAB-BTN-1","PT",440)
F.Intrinsic.Variable.SetProperty("GAB-BTN-1","PH",25)
F.Intrinsic.Variable.SetProperty("GAB-BTN-1","PW",100)
F.Intrinsic.Variable.SetProperty("GAB-BTN-1","FS",15)
F.Intrinsic.Variable.SetProperty("GAB-BTN-1","Caption","View Chg Hist")

F.Intrinsic.Control.CallSub(LabelsOff)
F.Intrinsic.Control.SelectCase(V.Caller.Hook)
	F.Intrinsic.Control.Case(12706)'Post clear
		F.Intrinsic.Control.CallSub(LabelsOff)
	F.Intrinsic.Control.Case(12201)'Part change
		F.Intrinsic.Control.CallSub(LabelsOff)
		F.Intrinsic.Control.End
	F.Intrinsic.Control.Case(12242)'SO Line delete
		F.Intrinsic.Control.CallSub(Delete)
	F.Intrinsic.Control.Case(12705)'SO Line change
		F.Intrinsic.Control.If(V.Passed.000011,<>,"00000000","AND",V.Passed.000052.Trim,<>,"")
			V.Global.sCPromDt.Set(V.Passed.000011)
			F.Intrinsic.Control.CallSub(CurrLine)
		F.Intrinsic.Control.ElseIf(V.Passed.000011,=,"00000000","AND",V.Passed.000052.Trim,<>,"")
			V.Global.sCPromDt.Set("19000101")
			V.Global.sNPromDt.Set("19000101")
			F.Intrinsic.Control.CallSub(CurrLine)
		F.Intrinsic.Control.Else
			F.Intrinsic.Control.CallSub(LabelsOff)
			F.Intrinsic.Control.End
		F.Intrinsic.Control.EndIf
	F.Intrinsic.Control.Case(12231)'SO Line post save
		F.Intrinsic.Control.If(V.Passed.000011,<>,"00000000","AND",V.Passed.000052.Trim,<>,"")
			V.Global.sCPromDt.Set(V.Passed.000011)
			F.Intrinsic.Control.CallSub(Update)
		F.Intrinsic.Control.ElseIf(V.Passed.000011,=,"00000000","AND",V.Passed.000052.Trim,<>,"")
			V.Global.sCPromDt.Set("19000101")
			V.Global.sNPromDt.Set("19000101")
			F.Intrinsic.Control.CallSub(Update)
		F.Intrinsic.Control.EndIf
	F.Intrinsic.Control.Case(12431)'GAB-BTN-1
		F.Intrinsic.Control.CallSub(DshBrd)
F.Intrinsic.Control.EndSelect


F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("({5}-{6})-Project: {4} Sub: {1} Error: {2}, {3}{0}", V.Ambient.NewLine, V.Ambient.CurrentSubroutine, V.Ambient.ErrorNumber, V.Ambient.ErrorDescription,V.Caller.ScriptFile,V.Ambient.Date, V.Ambient.Time,V.Local.sError)
	F.Intrinsic.Control.CallSub(Error, "Arg0", V.Local.sError)
F.Intrinsic.Control.EndTry
Program.Sub.Main.End

Program.Sub.CurrLine.Start
F.Intrinsic.Control.Try
V.Local.sError.Declare
V.Local.sSQL.Declare
V.Local.sRET.Declare
V.Local.sDateFormat.Declare(String, "")
V.Local.sFinal.Declare(String, "")
V.Local.sOrdNo.Declare
V.Local.sRecNo.Declare

F.Intrinsic.String.LPad(V.Passed.000060,0,7,V.Local.sOrdNo)
F.Intrinsic.String.LPad(V.Passed.000051,0,3,V.Local.sRecNo)
F.Intrinsic.String.RPad(V.Local.sRecNo,0,4,V.Local.sRecNo)

'Verify if an audit entry already exists
F.ODBC.Connection!conts.OpenCompanyConnection
F.Intrinsic.String.Build("select TOP 1 * from GCG_5654_AUDIT where ORDER_NO = '{0}' and RECORD_NO = '{1}' order by CHNG_COUNT desc",V.Local.sOrdNo.Trim,V.Local.sRecNo.Trim,V.Local.sSQL)
F.ODBC.Connection!conts.ExecuteAndReturn(V.Local.sSQL,V.Local.sRET)

'If an entry does not exist, insert one to collect the initial promise date
F.Intrinsic.Control.If(V.Ambient.ExecuteAndReturnEOF)
	F.Intrinsic.Control.If(v.Passed.000011,=,"00000000")
		F.Intrinsic.String.Build("insert into GCG_5654_AUDIT (ORDER_NO,RECORD_NO,ORIG_PROM_DT,NEW_PROM_DT,CHNG_COUNT,CHG_USER,CHG_REASON,DATE_CHNG) values ('{0}','{1}','{2}','{3}','{4}','{5}','Initial start date','{6}')",V.Local.sOrdNo.Trim,V.Local.sRecNo.Trim,V.Global.sCPromDt,V.Global.sNPromDt,0,V.Caller.User,V.Ambient.Date.PervasiveDate,V.Local.sSQL)
		V.Local.sDateFormat.Redim(0,2)
		F.Intrinsic.String.Left(V.Global.sCPromDt, 4, V.Local.sDateFormat(2))
		F.Intrinsic.String.Mid(V.Global.sCPromDt, 5, 2, V.Local.sDateFormat(0))
		F.Intrinsic.String.Right(V.Global.sCPromDt, 2, V.Local.sDateFormat(1))
		F.Intrinsic.String.Join(V.Local.sDateFormat, "/", V.Local.sFinal)
		V.Global.iChgCnt.Set(0)
	F.Intrinsic.Control.Else
		F.Intrinsic.String.Build("insert into GCG_5654_AUDIT (ORDER_NO,RECORD_NO,ORIG_PROM_DT,NEW_PROM_DT,CHNG_COUNT,CHG_USER,CHG_REASON,DATE_CHNG) values ('{0}','{1}','{2}','{3}','{4}','{5}','Initial start date','{6}')",V.Local.sOrdNo.Trim,V.Local.sRecNo.Trim,V.Passed.000011,V.Passed.000011,0,V.Caller.User,V.Ambient.Date.PervasiveDate,V.Local.sSQL)
		V.Local.sDateFormat.Redim(0,2)
		F.Intrinsic.String.Left(V.Passed.000011, 4, V.Local.sDateFormat(2))
		F.Intrinsic.String.Mid(V.Passed.000011, 5, 2, V.Local.sDateFormat(0))
		F.Intrinsic.String.Right(V.Passed.000011, 2, V.Local.sDateFormat(1))
		F.Intrinsic.String.Join(V.Local.sDateFormat, "/", V.Local.sFinal)
		V.Global.iChgCnt.Set(0)
	F.Intrinsic.Control.EndIf
F.Intrinsic.Control.Else
'If an entry does exist, collect original promise date
	F.Intrinsic.String.Split(V.Local.sRET,"*!*",V.Local.sRET)
	V.Global.sOPromDt.Set(V.Local.sRET(2))
	V.Local.sDateFormat.Redim(0,2)
	F.Intrinsic.String.Left(V.Global.sOPromDt, 4, V.Local.sDateFormat(2))
	F.Intrinsic.String.Mid(V.Global.sOPromDt, 5, 2, V.Local.sDateFormat(0))
	F.Intrinsic.String.Right(V.Global.sOPromDt, 2, V.Local.sDateFormat(1))
	F.Intrinsic.String.Join(V.Local.sDateFormat, "/", V.Local.sFinal)
	V.Global.iChgCnt.Set(V.Local.sRET(4))
F.Intrinsic.Control.EndIf

V.Passed.GAB-LBL-2.Set(V.Local.sFinal)
V.Passed.GAB-LBL-4.Set(V.Global.iChgCnt)

F.ODBC.Connection!conts.Execute(V.Local.sSQL)
F.ODBC.Connection!conts.Close

F.Intrinsic.Control.CallSub(LabelsOn)

F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("({5}-{6})-Project: {4} Sub: {1} Error: {2}, {3}{0}", V.Ambient.NewLine, V.Ambient.CurrentSubroutine, V.Ambient.ErrorNumber, V.Ambient.ErrorDescription,V.Caller.ScriptFile,V.Ambient.Date, V.Ambient.Time,V.Local.sError)
	F.Intrinsic.Control.CallSub(Error, "Arg0", V.Local.sError)
F.Intrinsic.Control.EndTry
Program.Sub.CurrLine.End

Program.Sub.Update.Start
F.Intrinsic.Control.Try
V.Local.sError.Declare
V.Local.sSQL.Declare
V.Local.sRET.Declare
V.Local.iRet.Declare
V.Local.sDateFormat.Declare
V.Local.sFinal.Declare
V.Local.sOrdNo.Declare
V.Local.sRecNo.Declare
V.Local.sRsn.Declare

F.Intrinsic.String.LPad(V.Passed.000060,0,7,V.Local.sOrdNo)
F.Intrinsic.String.LPad(V.Passed.000051,0,3,V.Local.sRecNo)
F.Intrinsic.String.RPad(V.Local.sRecNo,0,4,V.Local.sRecNo)

F.Intrinsic.Control.If(V.Passed.000011,=,"00000000")
	V.Global.sCPromDt.Set("19000101")
F.Intrinsic.Control.Else
	V.Global.sCPromDt.Set(V.Passed.000011)
F.Intrinsic.Control.EndIf

V.Global.sNPromDt.Set("19000101")
V.Global.sOPromDt.Set("19000101")

'Establish the new date from the screen
F.ODBC.Connection!conts.OpenCompanyConnection
F.Intrinsic.String.Build("select TOP 1 * from GCG_5654_AUDIT where ORDER_NO = '{0}' and RECORD_NO = '{1}' order by CHNG_COUNT desc",V.Local.sOrdNo.Trim,V.Local.sRecNo.Trim,V.Local.sSQL)
F.ODBC.Connection!conts.ExecuteAndReturn(V.Local.sSQL,V.Local.sRET)
	
'If there is no audit entry, create one.
F.Intrinsic.Control.If(V.Ambient.ExecuteAndReturnEOF)
	F.Intrinsic.String.Build("insert into GCG_5654_AUDIT (ORDER_NO,RECORD_NO,ORIG_PROM_DT,NEW_PROM_DT,CHNG_COUNT,CHG_USER,CHG_REASON,DATE_CHNG) values ('{0}','{1}','{2}','{3}','{4}','{5}','{6}','{7}')",V.Local.sOrdNo.Trim,V.Local.sRecNo.Trim,V.Global.sCPromDt,V.Global.sCPromDt,0,V.Caller.User,"Initial start date",V.Ambient.Date.PervasiveDate,V.Local.sSQL)
	V.Local.sDateFormat.Redim(0,2)
	F.Intrinsic.String.Left(V.Passed.000011, 4, V.Local.sDateFormat(2))
	F.Intrinsic.String.Mid(V.Passed.000011, 5, 2, V.Local.sDateFormat(0))
	F.Intrinsic.String.Right(V.Passed.000011, 2, V.Local.sDateFormat(1))
	F.Intrinsic.String.Join(V.Local.sDateFormat, "/", V.Local.sFinal)
	V.Passed.GAB-LBL-2.Set(V.Local.sFinal)
	F.Intrinsic.UI.ChangeCallerProperty(V.Passed.GAB-LBL-2,"HIDE",0)
	F.Intrinsic.UI.ChangeCallerProperty(V.Passed.GAB-LBL-2,"LOCK",0)
F.Intrinsic.Control.Else
	F.Intrinsic.String.Split(V.Local.sRET,"*!*",V.Local.sRET)
	V.Global.sOPromDt.Set(V.Local.sRET(2))
	V.Global.sNPromDt.Set(V.Local.sRET(3))
	V.Global.iChgCnt.Set(V.Local.sRET(4))
		F.Intrinsic.Control.If(V.Passed.000011,=,"00000000")
			V.Global.sCPromDt.Set("19000101")
		F.Intrinsic.Control.EndIf
	F.Intrinsic.Control.If(V.Global.sCPromDt,<>,V.Global.sNPromDt)
		F.Intrinsic.UI.Inputbox("Please enter a reason for this change:","The Promise Date has been modified","",V.Local.sRsn)

		F.Intrinsic.Control.If(V.Local.sRsn.Trim,=,"***CANCEL***")
			V.Local.sRsn.Set("")
		F.Intrinsic.Control.EndIf
		
		F.Intrinsic.Math.Add(V.Global.iChgCnt,1,V.Global.iChgCnt)
		F.Intrinsic.String.Build("insert into GCG_5654_AUDIT (ORDER_NO,RECORD_NO,ORIG_PROM_DT,NEW_PROM_DT,CHNG_COUNT,CHG_USER,CHG_REASON,DATE_CHNG) values ('{0}','{1}','{2}','{3}','{4}','{5}','{6}','{7}')",V.Local.sOrdNo.Trim,V.Local.sRecNo.Trim,V.Global.sOPromDt,V.Global.sCPromDt,V.Global.iChgCnt,V.Caller.User,V.Local.sRsn.PSQLFriendly,V.Ambient.Date.PervasiveDate,V.Local.sSQL)
		V.Passed.GAB-LBL-1.Set(" ")
		V.Passed.GAB-LBL-2.Set(" ")
	F.Intrinsic.Control.EndIf
F.Intrinsic.Control.EndIf

F.ODBC.Connection!conts.Execute(V.Local.sSQL)
F.ODBC.Connection!conts.Close

F.Intrinsic.Control.CallSub(LabelsOff)

F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("({5}-{6})-Project: {4} Sub: {1} Error: {2}, {3}{0}", V.Ambient.NewLine, V.Ambient.CurrentSubroutine, V.Ambient.ErrorNumber, V.Ambient.ErrorDescription,V.Caller.ScriptFile,V.Ambient.Date, V.Ambient.Time,V.Local.sError)
	F.Intrinsic.Control.CallSub(Error, "Arg0", V.Local.sError)
F.Intrinsic.Control.EndTry
Program.Sub.Update.End

Program.Sub.DshBrd.Start
F.Intrinsic.Control.Try
V.Local.sError.Declare
V.Local.sSQL.Declare
V.Local.iCnt.Declare
V.Local.sOrdNo.Declare
V.Local.sRecNo.Declare

F.Intrinsic.String.LPad(V.Passed.000060,0,7,V.Local.sOrdNo)
F.Intrinsic.String.LPad(V.Passed.000051,0,3,V.Local.sRecNo)
F.Intrinsic.String.RPad(V.Local.sRecNo,0,4,V.Local.sRecNo)

'Dashboard created for keeping track of promise date changes and to enter a reason for the change.
F.ODBC.Connection!conts.OpenCompanyConnection

F.Intrinsic.String.Build("select ORDER_NO,RECORD_NO,CONVERT(LEFT(CONVERT(ORIG_PROM_DT , SQL_CHAR),4) + '-' + RIGHT(LEFT(CONVERT(ORIG_PROM_DT , SQL_CHAR),6),2) + '-' + RIGHT(CONVERT(ORIG_PROM_DT , SQL_CHAR),2),SQL_DATE) as ORIG_PROM_DT,CONVERT(LEFT(CONVERT(NEW_PROM_DT , SQL_CHAR),4) + '-' + RIGHT(LEFT(CONVERT(NEW_PROM_DT , SQL_CHAR),6),2) + '-' + RIGHT(CONVERT(NEW_PROM_DT , SQL_CHAR),2),SQL_DATE) as NEW_PROM_DT,CHNG_COUNT,CHG_USER,CHG_REASON, DATE_CHNG from GCG_5654_AUDIT where ORDER_NO = '{0}' and RECORD_NO = '{1}' order by CHNG_COUNT desc",V.Local.sOrdNo.Trim,V.Local.sRecNo.Trim,V.Local.sSQL)
F.Data.DataTable.CreateFromSQL("dtChgHist","conts",V.Local.sSQL,True)

Gui.fmPDHist.GsPDHist.AddGridviewFromDatatable("gvChgHist","dtChgHist")

'Gui.fmPDHist.GsPDHist.SetGridviewProperty("gvChgHist","EnableAppearanceEvenRow",True)
Gui.fmPDHist.GsPDHist.SetGridviewProperty("gvChgHist","OptionsViewColumnAutoWidth", True)

Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","ORDER_NO","Caption","ORDER")
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","RECORD_NO","Caption","LINE")
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","ORIG_PROM_DT","Caption","ORIG PROMISE DATE")
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","NEW_PROM_DT","Caption","NEW PROMISE DATE")
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","CHNG_COUNT","Caption","CHANGE COUNT")
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","CHG_USER","Caption","CHANGE USER")
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","CHG_REASON","Caption","REASON")
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","DATE_CHNG","Caption","DATE CHANGED")

Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","ORDER_NO","HeaderFontBold",True)
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","RECORD_NO","HeaderFontBold",True)
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","ORIG_PROM_DT","HeaderFontBold",True)
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","NEW_PROM_DT","HeaderFontBold",True)
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","CHNG_COUNT","HeaderFontBold",True)
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","CHG_USER","HeaderFontBold",True)
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","CHG_REASON","HeaderFontBold",True)
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","DATE_CHNG","HeaderFontBold",True)

Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","ORDER_NO","HeaderHAlignment","Center")
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","RECORD_NO","HeaderHAlignment","Center")
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","ORIG_PROM_DT","HeaderHAlignment","Center")
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","NEW_PROM_DT","HeaderHAlignment","Center")
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","CHNG_COUNT","HeaderHAlignment","Center")
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","CHG_USER","HeaderHAlignment","Center")
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","CHG_REASON","HeaderHAlignment","Center")
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","DATE_CHNG","HeaderHAlignment","Center")

Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","ORDER_NO","CellHAlignment","Center")
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","RECORD_NO","CellHAlignment","Center")
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","ORIG_PROM_DT","CellHAlignment","Center")
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","NEW_PROM_DT","CellHAlignment","Center")
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","CHNG_COUNT","CellHAlignment","Center")
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","CHG_USER","CellHAlignment","Center")
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","DATE_CHNG","CellHAlignment","Center")

Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","ORDER_NO","Width","50")
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","RECORD_NO","Width","50")
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","ORIG_PROM_DT","Width","115")
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","NEW_PROM_DT","Width","115")
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","CHNG_COUNT","Width","75")
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","CHG_USER","Width","75")
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","CHG_REASON","Width","250")
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","DATE_CHNG","Width","100")

Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","ORDER_NO","ReadOnly",True)
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","ORDER_NO","AllowEdit",False)
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","RECORD_NO","ReadOnly",True)
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","RECORD_NO","AllowEdit",False)
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","ORIG_PROM_DT","ReadOnly",True)
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","ORIG_PROM_DT","AllowEdit",False)
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","NEW_PROM_DT","ReadOnly",True)
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","NEW_PROM_DT","AllowEdit",False)
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","CHNG_COUNT","ReadOnly",True)
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","CHNG_COUNT","AllowEdit",False)
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","CHG_USER","ReadOnly",True)
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","CHG_USER","AllowEdit",False)
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","CHG_REASON","ReadOnly",False)
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","CHG_REASON","AllowEdit",True)
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","DATE_CHNG","ReadOnly",False)
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","DATE_CHNG","AllowEdit",True)

Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","ORIG_PROM_DT","DisplayCustomDatetime",d)
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","NEW_PROM_DT","DisplayCustomDatetime",d)
Gui.fmPDHist.GsPDHist.SetColumnProperty("gvChgHist","DATE_CHNG","DisplayCustomDatetime",d)

Gui.fmPDHist.GsPDHist.AddStyleFormatCondition("gvChgHist","ORIG_PROM_DT","White","Equal","01/01/1900")
Gui.fmPDHist.GsPDHist.SetStyleFormatConditionProperty("gvChgHist","ORIG_PROM_DT","White","ForeColor","White")
Gui.fmPDHist.GsPDHist.SetStyleFormatConditionProperty("gvChgHist","ORIG_PROM_DT","White","BackColor","White")
Gui.fmPDHist.GsPDHist.AddStyleFormatCondition("gvChgHist","NEW_PROM_DT","White","Equal","01/01/1900")
Gui.fmPDHist.GsPDHist.SetStyleFormatConditionProperty("gvChgHist","NEW_PROM_DT","White","ForeColor","White")
Gui.fmPDHist.GsPDHist.SetStyleFormatConditionProperty("gvChgHist","NEW_PROM_DT","White","BackColor","White")

Gui.fmPDHist.GsPDHist.MainView("gvChgHist")
F.Intrinsic.Control.CallSub(SetIcon)
Gui.fmPDHist..Show

F.Intrinsic.Control.CallSub(LabelsOn)

F.ODBC.Connection!conts.Close

F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("({5}-{6})-Project: {4} Sub: {1} Error: {2}, {3}{0}", V.Ambient.NewLine, V.Ambient.CurrentSubroutine, V.Ambient.ErrorNumber, V.Ambient.ErrorDescription,V.Caller.ScriptFile,V.Ambient.Date, V.Ambient.Time,V.Local.sError)
	F.Intrinsic.Control.CallSub(Error, "Arg0", V.Local.sError)
F.Intrinsic.Control.EndTry
Program.Sub.DshBrd.End

Program.Sub.Delete.Start
F.Intrinsic.Control.Try
V.Local.sError.Declare
V.Local.sSQL.Declare
V.Local.sOrdNo.Declare
V.Local.sRecNo.Declare

F.Intrinsic.String.LPad(V.Passed.000060,0,7,V.Local.sOrdNo)
F.Intrinsic.String.LPad(V.Passed.000051,0,3,V.Local.sRecNo)
F.Intrinsic.String.RPad(V.Local.sRecNo,0,4,V.Local.sRecNo)

'Delete audit record entries when the SO Line is deleted.
F.ODBC.Connection!conts.OpenCompanyConnection
F.Intrinsic.String.Build("delete GCG_5654_AUDIT where ORDER_NO = '{0}' and RECORD_NO = '{1}'",V.Local.sOrdNo.Trim,V.Local.sRecNo.Trim,V.Local.sSQL)
F.ODBC.Connection!conts.Execute(V.Local.sSQL)
F.ODBC.Connection!conts.Close

F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("({5}-{6})-Project: {4} Sub: {1} Error: {2}, {3}{0}", V.Ambient.NewLine, V.Ambient.CurrentSubroutine, V.Ambient.ErrorNumber, V.Ambient.ErrorDescription,V.Caller.ScriptFile,V.Ambient.Date, V.Ambient.Time,V.Local.sError)
	F.Intrinsic.Control.CallSub(Error, "Arg0", V.Local.sError)
F.Intrinsic.Control.EndTry
Program.Sub.Delete.End

Program.Sub.GsPDHist_CellValueChanged.Start
F.Intrinsic.Control.Try
V.Local.sError.Declare
V.Local.sVAL.Declare
V.Local.sORD.Declare
V.Local.sLINE.Declare
V.Local.sCHG.Declare
V.Local.sSQL.Declare

'Allow updating of reason through dashboard.
F.ODBC.Connection!conts.OpenCompanyConnection
Gui.fmPDHist.GsPDHist.GetCellValueByColumnName("gvChgHist","CHG_REASON",V.Args.RowIndex,V.Local.sVAL)
Gui.fmPDHist.GsPDHist.GetCellValueByColumnName("gvChgHist","ORDER_NO",V.Args.RowIndex,V.Local.sORD)
Gui.fmPDHist.GsPDHist.GetCellValueByColumnName("gvChgHist","RECORD_NO",V.Args.RowIndex,V.Local.sLINE)
Gui.fmPDHist.GsPDHist.GetCellValueByColumnName("gvChgHist","CHNG_COUNT",V.Args.RowIndex,V.Local.sCHG)

F.Intrinsic.String.Build("update GCG_5654_AUDIT set CHG_REASON = '{0}' where ORDER_NO = '{1}' and RECORD_NO = '{2}' and CHNG_COUNT = '{3}'",V.Local.sVAL,V.Local.sORD,V.Local.sLINE,V.Local.sCHG,V.Local.sSQL)
F.ODBC.Connection!conts.Execute(V.Local.sSQL)

F.ODBC.Connection!conts.Close

F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("({5}-{6})-Project: {4} Sub: {1} Error: {2}, {3}{0}", V.Ambient.NewLine, V.Ambient.CurrentSubroutine, V.Ambient.ErrorNumber, V.Ambient.ErrorDescription,V.Caller.ScriptFile,V.Ambient.Date, V.Ambient.Time,V.Local.sError)
	F.Intrinsic.Control.CallSub(Error, "Arg0", V.Local.sError)
F.Intrinsic.Control.EndTry
Program.Sub.GsPDHist_CellValueChanged.End

Program.Sub.LabelsOn.Start
F.Intrinsic.Control.Try
V.Local.sError.Declare

V.Passed.GAB-LBL-1.Set("Original Promise Date: ")
V.Passed.GAB-LBL-3.Set("Current Change Count: ")

F.Intrinsic.UI.ChangeCallerProperty(V.Passed.GAB-BTN-1,"HIDE",0)
F.Intrinsic.UI.ChangeCallerProperty(V.Passed.GAB-BTN-1,"LOCK",0)
F.Intrinsic.UI.ChangeCallerProperty(V.Passed.GAB-LBL-1,"HIDE",0)
F.Intrinsic.UI.ChangeCallerProperty(V.Passed.GAB-LBL-1,"LOCK",0)
F.Intrinsic.UI.ChangeCallerProperty(V.Passed.GAB-LBL-2,"HIDE",0)
F.Intrinsic.UI.ChangeCallerProperty(V.Passed.GAB-LBL-2,"LOCK",0)
F.Intrinsic.UI.ChangeCallerProperty(V.Passed.GAB-LBL-3,"HIDE",0)
F.Intrinsic.UI.ChangeCallerProperty(V.Passed.GAB-LBL-3,"LOCK",0)
F.Intrinsic.UI.ChangeCallerProperty(V.Passed.GAB-LBL-4,"HIDE",0)
F.Intrinsic.UI.ChangeCallerProperty(V.Passed.GAB-LBL-4,"LOCK",0)

F.Intrinsic.Control.If(V.Caller.Switches,=,"O")
	F.Intrinsic.UI.ChangeCallerProperty(V.Passed.GAB-BTN-1,"HIDE",0)
	F.Intrinsic.UI.ChangeCallerProperty(V.Passed.GAB-BTN-1,"LOCK",0)
F.Intrinsic.Control.EndIf

F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("({5}-{6})-Project: {4} Sub: {1} Error: {2}, {3}{0}", V.Ambient.NewLine, V.Ambient.CurrentSubroutine, V.Ambient.ErrorNumber, V.Ambient.ErrorDescription,V.Caller.ScriptFile,V.Ambient.Date, V.Ambient.Time,V.Local.sError)
	F.Intrinsic.Control.CallSub(Error, "Arg0", V.Local.sError)
F.Intrinsic.Control.EndTry
Program.Sub.LabelsOn.End

Program.Sub.LabelsOff.Start
F.Intrinsic.Control.Try
V.Local.sError.Declare

'Only allow reasons entry on SO Line edit screen

F.Intrinsic.UI.ChangeCallerProperty(V.Passed.GAB-BTN-1,"HIDE",1)

'Setting labels on SO Line screen

F.Intrinsic.UI.ChangeCallerProperty(V.Passed.GAB-LBL-1,"HIDE",1)
F.Intrinsic.UI.ChangeCallerProperty(V.Passed.GAB-LBL-2,"HIDE",1)
F.Intrinsic.UI.ChangeCallerProperty(V.Passed.GAB-LBL-3,"HIDE",1)
F.Intrinsic.UI.ChangeCallerProperty(V.Passed.GAB-LBL-4,"HIDE",1)

F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("({5}-{6})-Project: {4} Sub: {1} Error: {2}, {3}{0}", V.Ambient.NewLine, V.Ambient.CurrentSubroutine, V.Ambient.ErrorNumber, V.Ambient.ErrorDescription,V.Caller.ScriptFile,V.Ambient.Date, V.Ambient.Time,V.Local.sError)
	F.Intrinsic.Control.CallSub(Error, "Arg0", V.Local.sError)
F.Intrinsic.Control.EndTry
Program.Sub.LabelsOff.End

Program.Sub.SetIcon.Start
F.Intrinsic.Control.Try
V.Local.sError.Declare
V.Local.sForm.Declare(String)
V.Local.bExists.Declare(Boolean)
V.Local.sIcon.Declare(String)
V.Local.iCounter.Declare(Long)

F.Intrinsic.String.Build("{0}\ART\gss2.ico",V.Caller.GlobalDir,v.Local.sIcon)
F.Intrinsic.File.Exists(V.Local.sIcon, V.Local.bExists)
F.Intrinsic.Control.If(V.Local.bExists.Not)
    F.Intrinsic.String.Build("{0}\GAB\GAS\gss2.ico",V.Caller.PluginsDir,v.Local.sIcon)
    F.Intrinsic.File.Exists(V.Local.sIcon, V.Local.bExists)
    F.Intrinsic.Control.If(V.Local.bExists.Not)
        F.Intrinsic.Control.ExitSub
    F.Intrinsic.Control.EndIf
F.Intrinsic.Control.EndIf

F.Intrinsic.UI.GetFormList(V.Local.sForm)
F.Intrinsic.String.Split(V.Local.sForm, "*!*", V.Local.sForm)

F.Intrinsic.Control.For(V.Local.iCounter, 0, V.Local.sForm.UBound, 1)
    Gui.[V.Local.sForm(V.Local.iCounter)]..Icon(V.Local.sIcon)
F.Intrinsic.Control.Next(V.Local.iCounter)

F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("({5}-{6})-Project: {4} Sub: {1} Error: {2}, {3}{0}", V.Ambient.NewLine, V.Ambient.CurrentSubroutine, V.Ambient.ErrorNumber, V.Ambient.ErrorDescription,V.Caller.ScriptFile,V.Ambient.Date, V.Ambient.Time,V.Local.sError)
	F.Intrinsic.Control.CallSub(Error, "Arg0", V.Local.sError)
F.Intrinsic.Control.EndTry
Program.Sub.SetIcon.End

Program.Sub.Error.Start
V.Local.iRet.Declare
F.Intrinsic.UI.Msgbox(V.Args.Arg0,"Error",V.Local.iRet)
F.Intrinsic.Control.End
Program.Sub.Error.End

Program.Sub.fmPDHist_UnLoad.Start
F.Intrinsic.Control.End
Program.Sub.fmPDHist_UnLoad.End
